apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion versions.compile_sdk

    defaultConfig {
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        versionCode 8
        versionName "2.1.0"
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        // Enable experimental Kotlin features in gradle to enable Parcelize
        // https://github.com/Kotlin/KEEP/blob/master/proposals/extensions/android-parcelable.md
        experimental = true
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
}

publish {
    Properties properties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    userOrg = properties.containsKey('bintray.user') ? properties.getProperty("bintray.user") : ""
    groupId = 'com.uploadcare.android.widget'
    artifactId = 'uploadcare-android-widget'
    publishVersion = '2.1.0'
    desc = 'Android client library for the Uploadcare API.'
    licences = ["Apache-2.0"]
    website = 'https://github.com/uploadcare/uploadcare-android'
    issueTracker = 'https://github.com/uploadcare/uploadcare-android/issues'
    repository = 'https://github.com/uploadcare/uploadcare-android.git'
    autoPublish = true
    bintrayUser = properties.containsKey('bintray.user') ? properties.getProperty("bintray.user") : ""
    bintrayKey = properties.containsKey('bintray.key') ? properties.getProperty("bintray.key") : ""
    dryRun = true
}

dependencies {
    api project(':library')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin_version"

    implementation "androidx.core:core-ktx:$versions.core"

    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"

    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    implementation "com.squareup.moshi:moshi:$versions.moshi"
    implementation "com.squareup.moshi:moshi-adapters:$versions.moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"

    implementation "com.squareup.picasso:picasso:$versions.picasso"

    androidTestImplementation "androidx.test:runner:$versions.test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso_core"
}
